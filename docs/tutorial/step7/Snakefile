import snakebids
from snakebids import bids

configfile: 'config.yml'

inputs = snakebids.generate_inputs(
    bids_dir=config['bids_dir'],
    pybids_inputs=config['pybids_inputs'],
    use_bids_inputs=True,
)

rule all:
    input:
        expand(
            bids(
                root='results',
                fwhm='{fwhm}',
                suffix='bold.nii.gz',
                **inputs.input_wildcards['bold']
            ),
            fwhm=config['fwhm'],
            **inputs.input_lists['bold']
        )


def calc_sigma_from_fwhm(wildcards):
    return f'{float(wildcards.fwhm)/2.355:0.2f}'


rule smooth:
    input:
        inputs.input_path['bold']
    params:
        sigma = calc_sigma_from_fwhm
    output:
        bids(
            root='results',
            fwhm='{fwhm}',
            suffix='bold.nii.gz',
            **inputs.input_wildcards['bold']
        )
    shell:
        'fslmaths {input} -s {params.sigma} {output}'
